;--------------------------------------
;蜂起イベントが発生するかの判定
;--------------------------------------
@VIGILANTE_RISE_RATE()
;既に出現しているか、自警団なし設定なら発動しない
SIF SP_COUNTRY_APPEARED:特殊勢力_自警団 || !SP_COUNTRY_RANK:(特殊勢力_自警団)
	RETURN 0

;いっぱいなら駄目
SIF GET_NEW_COUNTRY() == -1
	RETURN 0

SIF !CHECK_SP_COUNTRY_RISE(特殊勢力_自警団)
	RETURN 0
RETURN 1


;--------------------------------------
;自警団出現イベント
;出現できたら1、できなければ0をかえす
;--------------------------------------
@VIGILANTE_RISE(都市 = 0)
#DIM 自警団
#DIM 都市
;自警団勢力を作成
CALL VIGILANTE_INIT()
自警団 = RESULT

;作れなかったら戻す
SIF 自警団 == -1
	RETURN 0

CALL SP_COUNTRY_RISE(自警団, 都市)

SIF !RESULT
	RETURN 0

SP_COUNTRY_APPEARED:特殊勢力_自警団 = 1
RETURN 自警団

;--------------------------------------
;蜂起イベント用都市選択処理
;--------------------------------------
@VIGILANTE_RISE_SELECT_CITY()
#DIM 住宅街
住宅街 = GET_CITYNUMBER("人里住宅街")
IF 住宅街 >= 0 && (!IS_COUNTRY(CITY_OWNER:住宅街) || GET_OWN_CITY(CITY_OWNER:住宅街) >= 2)
	LOCAL = 住宅街
ELSE
	CALL SP_COUNTRY_RISE_SELECT_CITY()
	LOCAL = RESULT
ENDIF
RETURN LOCAL

;--------------------------------------
;蜂起イベント用メッセージ関数
;--------------------------------------
@VIGILANTE_RISE_MSG(勢力, 対象)
#DIM 勢力
#DIM 対象
CALL SINGLE_DRAWLINE
SETCOLOR カラー_警告
PRINTFORMW 担忧幻想乡变得荒芜的勇士们，在%CITY_NAME:(対象)%\@ GET_OWN_CITY(勢力) > 1 ? 等地 # \@成立了自警团！
;TranslatedMark 擔憂幻想鄉変得荒芜的勇士們、在%CITY_NAME:(対象)%\@ GET_OWN_CITY(勢力) > 1 ? 等地 # \@成立了自警団！
PRINTFORMW 认为只有自己才能和平地治理幻想乡的他们，似乎想要进一步扩张势力
;TranslatedMark 自分たちこそ幻想郷を平和に治められると考えた彼らは、より勢力を伸ばそうとしているようです
PRINTFORMW 「当地人的笑容比什么都重要"、"为了维持治安而粉身碎骨"等口号，传播着怪异的思想……
;TranslatedMark 「地元の人たちの笑顔が何より」「治安維持のため粉骨砕身」などをモットーに、怪しげな思想を広めています……
RESETCOLOR
